/*
 * Licensed to the Apache Software Foundation (ASF) under one
 * or more contributor license agreements.  See the NOTICE file
 * distributed with this work for additional information
 * regarding copyright ownership.  The ASF licenses this file
 * to you under the Apache License, Version 2.0 (the
 * "License"); you may not use this file except in compliance
 * with the License.  You may obtain a copy of the License at
 *
 * http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing,
 * software distributed under the License is distributed on an
 * "AS IS" BASIS, WITHOUT WARRANTIES OR CONDITIONS OF ANY
 * KIND, either express or implied.  See the License for the
 * specific language governing permissions and limitations
 * under the License.
 */

plugins {
    id 'com.gradle.enterprise' version '3.13.3'
    id 'com.gradle.common-custom-user-data-gradle-plugin' version '1.10'
}

def isCI = System.getenv('GITHUB_ACTIONS') != null

gradleEnterprise {
    server = "https://ge.apache.org"
    buildScan {
        capture { taskInputFiles = true }
        uploadInBackground = !isCI
        publishAlways()
        publishIfAuthenticated()
        obfuscation {
            // This obfuscates the IP addresses of the build machine in the build scan.
            // Alternatively, the build scan will provide the hostname for troubleshooting host-specific issues.
            ipAddresses { addresses -> addresses.collect { address -> "0.0.0.0"} }
        }
    }
}

buildCache {
    local {
        enabled = !isCI
    }

    remote(gradleEnterprise.buildCache) {
        enabled = false
    }
}

apply from: 'common.gradle'
rootProject.name = 'ofbiz'

def fileSep = System.getProperty('file.separator')
def isWindows = System.getProperty('os.name').toLowerCase().contains('windows')

activeComponents().each { File component ->
    def subProject = (component.toString() - rootDir)
    if (isWindows) 
        include subProject.replaceAll('\\' + fileSep, ':') 
    else 
        include subProject.replaceAll(fileSep, ':')
    
}
