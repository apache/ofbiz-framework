/*
 * Licensed to the Apache Software Foundation (ASF) under one
 * or more contributor license agreements.  See the NOTICE file
 * distributed with this work for additional information
 * regarding copyright ownership.  The ASF licenses this file
 * to you under the Apache License, Version 2.0 (the
 * "License"); you may not use this file except in compliance
 * with the License.  You may obtain a copy of the License at
 *
 * http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing,
 * software distributed under the License is distributed on an
 * "AS IS" BASIS, WITHOUT WARRANTIES OR CONDITIONS OF ANY
 * KIND, either express or implied.  See the License for the
 * specific language governing permissions and limitations
 * under the License.
 */

plugins {
    id 'groovy'
    id 'eclipse'
    id 'checkstyle'
    id 'maven-publish'
}

sourceCompatibility = '1.8'
targetCompatibility = '1.8'

dependencies {
    implementation project(":framework")

    implementation 'xerces:xercesImpl:2.12.1'
    implementation 'com.google.zxing:core:3.4.1'
    implementation 'com.googlecode.concurrentlinkedhashmap:concurrentlinkedhashmap-lru:1.4.2'
    implementation 'com.googlecode.ez-vcard:ez-vcard:0.11.2'
    implementation 'com.googlecode.owasp-java-html-sanitizer:owasp-java-html-sanitizer:20200713.1'
    implementation 'com.googlecode.libphonenumber:libphonenumber:8.12.3'
    implementation 'com.ibm.icu:icu4j:69.1'
    implementation 'com.lowagie:itext:2.1.7' // Don't update due to license change in newer versions, see OFBIZ-10455
    implementation 'com.sun.mail:javax.mail:1.6.2'
    implementation 'com.rometools:rome:1.16.0'
    implementation 'com.thoughtworks.xstream:xstream:1.4.18'
    implementation 'commons-fileupload:commons-fileupload:1.4'
    implementation 'commons-net:commons-net:3.8.0'
    implementation 'commons-validator:commons-validator:1.7'
    implementation 'de.odysseus.juel:juel-impl:2.2.7'
    implementation 'net.fortuna.ical4j:ical4j:1.0-rc4-atlassian-12'
    implementation 'net.lingala.zip4j:zip4j:2.9.0'
    implementation 'org.apache.ant:ant-junit:1.10.11'
    implementation 'org.apache.commons:commons-collections4:4.4'
    implementation 'org.apache.commons:commons-dbcp2:2.9.0'
    implementation 'org.apache.commons:commons-imaging:1.0-alpha2'
    // Alpha but OK, "Imaging was working and was used by a number of projects in production even before reaching its initial release as an Apache Commons component."
    implementation 'org.apache.commons:commons-text:1.9'
    implementation 'org.apache.geronimo.components:geronimo-transaction:3.1.4'
    implementation 'org.apache.geronimo.specs:geronimo-jms_1.1_spec:1.1.1'
    implementation 'org.apache.httpcomponents:httpclient-cache:4.5.13'
    implementation 'org.apache.logging.log4j:log4j-api:2.14.1' // the API of log4j 2
    implementation 'org.apache.logging.log4j:log4j-core:2.14.1'
    // Somehow needed by Buildbot to compile OFBizDynamicThresholdFilter.java
    implementation 'org.apache.poi:poi:4.1.2' // poi-ooxml-schemas-5.0.0.pom'. Received status code 401 from server
    implementation 'org.apache.pdfbox:pdfbox:2.0.24'
    implementation 'org.apache.shiro:shiro-core:1.8.0'
    implementation 'org.apache.sshd:sshd-core:1.7.0'
    // So far we did not update from 1.7.0 because of a compile issue. You may try w/ a newer version than  2.4.0
    implementation 'org.apache.tika:tika-core:1.26' //  2.1.0 does not work
    implementation 'org.apache.tika:tika-parsers:1.25' //  2.1.0 does not work
    implementation 'org.apache.tomcat:tomcat-catalina-ha:9.0.54'
    // Remember to change the version number (9 now) in javadoc block if needed.
    implementation 'org.apache.tomcat:tomcat-jasper:9.0.54'
    implementation 'org.apache.axis2:axis2-kernel:1.7.9'
    // Above: SOAPEventHandler.java:42: error: package org.apache.axiom.om.impl.builder does not exist
    implementation 'batik:batik-svg-dom:1.6-1'
    implementation 'org.apache.xmlgraphics:fop:2.3'
    // NOTE: since 2.4 dependencies are messed up. See https://github.com/moqui/moqui-fop/blob/master/build.gradle
    implementation 'org.apache.xmlrpc:xmlrpc-client:3.1.3'
    implementation 'org.apache.xmlrpc:xmlrpc-server:3.1.3'
    implementation 'org.clojure:clojure:1.10.2'
    implementation 'org.codehaus.groovy:groovy-all:2.5.11'
    // Compile issue with commons-cli and Groovy 3. Remember to change the version number in javadoc block.
    implementation 'org.freemarker:freemarker:2.3.31'
    // Remember to change the version number in FreeMarkerWorker class when upgrading. See OFBIZ-10019 if >= 2.4
    implementation 'org.owasp.esapi:esapi:2.2.2.0'
    implementation 'org.springframework:spring-test:5.3.9'
    implementation 'org.zapodot:jackson-databind-java-optional:2.6.1'
    implementation 'oro:oro:2.0.8'
    implementation 'wsdl4j:wsdl4j:1.6.3'
    implementation 'com.auth0:java-jwt:3.18.1'
    implementation 'org.jdom:jdom:1.1.3'
    // don't upgrade above 1.1.3, makes a lot of not obvious and useless complications, see last commits of OFBIZ-12092 for more

    runtimeOnly 'javax.xml.soap:javax.xml.soap-api:1.4.0'
    runtimeOnly 'de.odysseus.juel:juel-spi:2.2.7'
    runtimeOnly 'net.sf.barcode4j:barcode4j-fop-ext:2.1'
    runtimeOnly 'net.sf.barcode4j:barcode4j:2.1'
    runtimeOnly 'org.apache.axis2:axis2-transport-http:1.7.9'
    // Above: SOAPEventHandler.java:42: error: package org.apache.axiom.om.impl.builder does not exist
    runtimeOnly 'org.apache.axis2:axis2-transport-local:1.7.9'
    // Above: SOAPEventHandler.java:42: error: package org.apache.axiom.om.impl.builder does not exist
    runtimeOnly 'org.apache.derby:derby:10.14.2.0'
    // So far we did not update from 10.14.2.0 because of a compile issue. You may try w/ a newer version than 10.15.1.3
    runtimeOnly 'org.apache.geronimo.specs:geronimo-jaxrpc_1.1_spec:2.1'
    runtimeOnly 'org.apache.logging.log4j:log4j-1.2-api:2.14.1'
    // for external jars using the old log4j1.2: routes logging to log4j 2
    runtimeOnly 'org.apache.logging.log4j:log4j-jul:2.14.1'
    // for external jars using the java.util.logging: routes logging to log4j 2
    runtimeOnly 'org.apache.logging.log4j:log4j-slf4j-impl:2.14.1'
    // for external jars using slf4j: routes logging to log4j 2
    runtimeOnly 'org.apache.logging.log4j:log4j-jcl:2.14.1'
    // need to constrain to version to avoid classpath conflict (ReflectionUtil)
    runtimeOnly 'org.codeartisans.thirdparties.swing:batik-all:1.8pre-r1084380'

}

def excludedJavaSources = [
        'org/apache/ofbiz/accounting/thirdparty/cybersource/IcsPaymentServices.java',
        'org/apache/ofbiz/accounting/thirdparty/orbital/OrbitalPaymentServices.java',
        'org/apache/ofbiz/accounting/thirdparty/paypal/PayPalServices.java',
        'org/apache/ofbiz/accounting/thirdparty/securepay/SecurePayPaymentServices.java',
        'org/apache/ofbiz/accounting/thirdparty/securepay/SecurePayServiceTest.java',
        'org/apache/ofbiz/accounting/thirdparty/verisign/PayflowPro.java',
        'org/apache/ofbiz/order/thirdparty/taxware/TaxwareException.java',
        'org/apache/ofbiz/order/thirdparty/taxware/TaxwareServices.java',
        'org/apache/ofbiz/order/thirdparty/taxware/TaxwareUTL.java'
]

sourceSets {
    main {
        java {
            srcDirs = ['party/src/main/java',
                       'securityext/src/main/java',
                       'content/src/main/java',
                       'workeffort/src/main/java',
                       'product/src/main/java',
                       'manufacturing/src/main/java',
                       'accounting/src/main/java',
                       'humanres/src/main/java',
                       'order/src/main/java',
                       'marketing/src/main/java'
            ]
            exclude excludedJavaSources
        }
        groovy {
            srcDirs = ['party/src/main/groovy',
                       'content/src/main/groovy',
                       'product/src/main/groovy',
                       'accounting/src/main/groovy',
                       'order/src/main/groovy',
                       'marketing/src/main/groovy']
        }
        resources {
            srcDirs = ['content/src/main/resources',
                       'product/src/main/resources']

            srcDirs += ['party/config',
                        'securityext/config',
                        'content/config',
                        'workeffort/config',
                        'product/config',
                        'manufacturing/config',
                        'accounting/config',
                        'humanres/config',
                        'order/config',
                        'marketing/config',
                        'commonext/config',]
            srcDirs += ['content/dtd',
                        'product/dtd',]
        }
    }

    groovyScripts {
        groovy {
            srcDirs += ['party/groovyScripts',
                        'content/groovyScripts',
                        'workeffort/groovyScripts',
                        'product/groovyScripts',
                        'manufacturing/groovyScripts',
                        'accounting/groovyScripts',
                        'humanres/groovyScripts',
                        'order/groovyScripts',
                        'marketing/groovyScripts',
                        'commonext/groovyScripts',]
            compileClasspath += sourceSets.main.compileClasspath
            compileClasspath += sourceSets.main.output
        }
    }
}

tasks.named('compileGroovyScriptsGroovy') {
    // We don't want to build groovyScripts as they should be considered as standalone elements executed in
    // isolation by ofbiz. Building them will result in numerous error due to duplicated classes.
    enabled = false
}
